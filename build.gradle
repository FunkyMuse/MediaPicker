ext {
    lifecycle = '2.3.0-rc01'
    glide = "4.11.0"
    constraint_layout = '2.0.4'
    material = "1.3.0-beta01"
    fragment  = "1.3.0-rc01"
    activity = "1.2.0-rc01"
    recycler = "1.0.0"
    appCompat = "1.2.0"
    coreKTX = "1.5.0-alpha05"

    //compilation
    compileVersion = 30
    minVersion = 21
    verCode = 1
    verName = "1.0.0"
    testRunner = "androidx.test.runner.AndroidJUnitRunner"
}

buildscript {
    ext.kotlin_version = '1.4.21'

    ext.compiledAppVersion = 30

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

allprojects {
    group = "io.github.funkymuse"

    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}



subprojects {

    switch (it.name){
        case 'app':
            apply plugin: 'com.android.application'
            apply plugin: 'kotlin-android'
            apply plugin: 'kotlin-kapt'
            apply plugin: 'kotlin-parcelize'

            dependencies {
                implementation fileTree(dir: 'libs', include: ['*.jar'])
                implementation project(path: ':imagepicker')
                implementation project(path: ':videopicker')
                implementation project(path: ':audiopicker')
                implementation project(path: ':core')
            }

            break

        case 'core':

            apply plugin: 'com.android.library'
            apply plugin: 'kotlin-android'
            apply plugin: 'kotlin-parcelize'
            apply plugin: 'com.github.dcendents.android-maven'
            apply plugin: 'kotlin-kapt'

            dependencies {
                implementation fileTree(dir: 'libs', include: ['*.jar'])
                testImplementation 'junit:junit:4.13.1'
                androidTestImplementation 'androidx.test.ext:junit:1.1.2'
                androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

                //glide
                api "com.github.bumptech.glide:glide:$glide"
                kapt "com.github.bumptech.glide:compiler:$glide"

                //core
                api "androidx.activity:activity-ktx:$activity"
                api "androidx.fragment:fragment-ktx:$fragment"
                api "androidx.appcompat:appcompat:$appCompat"
                api "androidx.core:core-ktx:$coreKTX"

                //ui
                api "androidx.constraintlayout:constraintlayout:$constraint_layout"
                api "androidx.recyclerview:recyclerview:$recycler"
                api "com.google.android.material:material:$material"

                //live data
                api "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle"
                api "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle"
                api "androidx.lifecycle:lifecycle-common-java8:$lifecycle"
                api "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle"
            }
            break

        case 'extensions':


            apply plugin: 'com.android.library'
            apply plugin: 'kotlin-android'
            apply plugin: 'kotlin-parcelize'
            apply plugin: 'com.github.dcendents.android-maven'
            apply plugin: 'kotlin-kapt'

            dependencies {
                implementation fileTree(dir: 'libs', include: ['*.jar'])
                implementation project(path: ':core')
            }

            break
            default:

            apply plugin: 'com.android.library'
            apply plugin: 'kotlin-android'
            apply plugin: 'kotlin-parcelize'
            apply plugin: 'com.github.dcendents.android-maven'
            apply plugin: 'kotlin-kapt'

            dependencies {
                implementation fileTree(dir: 'libs', include: ['*.jar'])
                implementation project(path: ':core')
                implementation project(path: ':extensions')
            }


    }

    android {
        compileSdkVersion compileVersion

        defaultConfig {
            minSdkVersion minVersion
            targetSdkVersion compileVersion
            versionCode verCode
            versionName verName
            testInstrumentationRunner testRunner
        }

        compileOptions {
            sourceCompatibility = 1.8
            targetCompatibility = 1.8
        }

        kotlinOptions {
            jvmTarget = "1.8"
        }

        buildFeatures {
            viewBinding = true
            aidl = false
            renderScript = false
            resValues = false
            shaders = false
            buildConfig = false
        }
    }
}
